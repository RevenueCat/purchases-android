apply from: "$rootProject.projectDir/library.gradle"

ext {
    iapVersion = "2.0.76"
}

dependencies {
    implementation project(":purchases")
    implementation project(":common")
    implementation project(":strings")
    implementation project(":utils")

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    compileOnly files("libs/in-app-purchasing-${iapVersion}.jar")

    testImplementation files("libs/in-app-purchasing-${iapVersion}.jar")
    testImplementation project(":test-utils")
}

task downloadAmazonZipFile(type: Download) {
    src 'https://amzndevresources.com/iap/sdk/AmazonInAppPurchasing_Android.zip'
    dest new File(buildDir, 'download.zip')
}

task getAmazonLibrary(dependsOn: downloadAmazonZipFile, type: Copy) {
    ext {
        fileToExtract = "in-app-purchasing-${iapVersion}.jar"
        destFile = new File( projectDir, "libs/$fileToExtract" )
    }
    File destDir = destFile.parentFile
    destDir.mkdirs()
    project.copy {
        from {
            zipTree(downloadAmazonZipFile.dest).matching { include "**/$fileToExtract" }.singleFile
        }

        into( destDir )
    }
}


preBuild.dependsOn getAmazonLibrary