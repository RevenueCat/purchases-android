import org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask

plugins {
    alias libs.plugins.android.library
    alias libs.plugins.dokka
    alias libs.plugins.kotlin.android
    alias libs.plugins.kotlin.serialization
    alias libs.plugins.kotlin.parcelize
    alias libs.plugins.paparazzi
    alias libs.plugins.poko
}


if (!project.getProperties()["ANDROID_VARIANT_TO_PUBLISH"].contains("customEntitlementComputation")) {
    apply plugin: "com.vanniktech.maven.publish"
}

apply from: "$rootProject.projectDir/library.gradle"

android {
    namespace 'com.revenuecat.purchases.ui.revenuecatui'

    flavorDimensions = ["apis"]
    productFlavors {
        defaults {
            dimension "apis"
        }
    }

    defaultConfig {
        minSdkVersion 24 // MeasureFormat requires API 24
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.8"
    }
    packaging {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
        resources.excludes.add("META-INF/LICENSE.md")
        resources.excludes.add("META-INF/LICENSE-notice.md")
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    debugImplementation libs.emerge.snapshots.annotations

    implementation project(path: ':purchases')

    implementation(platform(libs.kotlin.bom))
    implementation libs.androidx.core
    implementation platform(libs.compose.bom)
    implementation libs.compose.ui
    implementation libs.compose.ui.util
    implementation libs.compose.ui.graphics
    implementation libs.compose.ui.tooling.preview
    implementation libs.compose.material
    implementation libs.compose.material3
    implementation libs.compose.material3Adaptive
    implementation libs.compose.constraintlayout
    implementation libs.window
    implementation libs.window.core
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.lifecycle.viewmodel
    implementation libs.androidx.lifecycle.viewmodel.compose
    implementation libs.coil.compose
    implementation libs.coil.svg
    implementation libs.commonmark
    implementation libs.commonmark.strikethrough
    implementation libs.activity.compose
    implementation libs.androidx.fragment.ktx
    implementation libs.compose.ui.google.fonts
    implementation libs.androidx.browser
    debugImplementation libs.compose.ui.tooling
    debugImplementation libs.androidx.test.compose.manifest

    testImplementation libs.bundles.test
    testImplementation libs.coil.test
    testImplementation libs.coroutines.test
    testImplementation libs.kotlinx.serialization.json
    testImplementation libs.androidx.test.compose
    testImplementation libs.androidx.test.compose.manifest
    testImplementation libs.hamcrest.core

    androidTestImplementation libs.assertJ
    androidTestImplementation libs.mockk.android
    androidTestImplementation libs.mockk.agent
    androidTestImplementation libs.androidx.test.compose
}

tasks.dokkaHtmlPartial.configure {
    dokkaSourceSets {
        named("main") {
            reportUndocumented.set(true)
            includeNonPublic.set(false)
            skipDeprecated.set(true)

            externalDocumentationLink {
                url.set(uri("https://developer.android.com/reference/package-list").toURL())
            }
            sourceLink {
                localDirectory.set(file("src/main/kotlin"))
                remoteUrl.set(uri("https://github.com/revenuecat/purchases-android/blob/main/ui/revenuecatui/src/main/kotlin").toURL())
                remoteLineSuffix.set("#L")
            }
        }
    }
}

// Opt-in to InternalRevenueCatAPI for the entire module. This avoids having to explicitly opt in at every call site
// separately. Lint is handled by ui/revenuecatui/lint.xml.
tasks.withType(KotlinCompilationTask).configureEach {
    compilerOptions {
        freeCompilerArgs.add("-opt-in=com.revenuecat.purchases.InternalRevenueCatAPI")
    }
}
