buildscript {
    ext.kotlinVersion = "1.4.10"
    ext.compileVersion = 28
    ext.minVersion = 14
    ext.billingVersion = "4.0.0"
    ext.lifecycleVersion = "2.3.0-rc01"
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath "com.vanniktech:gradle-maven-publish-plugin:0.15.1"
        classpath "com.android.tools.build:gradle:4.1.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:1.4.32"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.17.0-RC3"
    id "com.github.kt3k.coveralls" version "2.10.0"
    id "com.savvasdalkitsis.module-dependency-graph" version "0.9"
}

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.17.0-RC3"
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task detektAll(type: io.gitlab.arturbosch.detekt.Detekt) {
    description = "Runs over whole code base without the starting overhead for each module."
    buildUponDefaultConfig = true
    autoCorrect = true
    parallel = true
    setSource(files(rootDir))
    include("**/*.kt")
    include("**/*.kts")
    exclude("**/build/**")
    exclude("**/test/**/*.kt")
    exclude("examples/MagicWeather/**")
    baseline.set(file("$rootDir/config/detekt/detekt-baseline.xml"))
    reports {
        xml.enabled = true
        xml.destination = file("build/reports/detekt/detekt.xml")
        html.enabled = false
        txt.enabled = false
    }
}

task detektAllBaseline(type: io.gitlab.arturbosch.detekt.DetektCreateBaselineTask) {
    description = "Overrides current top level baseline with issues found on this run." +
            "Issues found on the baseline will be ignored on detekt runs."
    buildUponDefaultConfig.set(true)
    ignoreFailures.set(true)
    parallel.set(true)
    setSource(files(rootDir))
    baseline.set(file("$rootDir/config/detekt/detekt-baseline.xml"))
    include("**/*.kt")
    include("**/*.kts")
    exclude("**/build/**")
    exclude("**/test/**/*.kt")
}

apply plugin: 'org.jetbrains.dokka'

tasks.dokkaHtmlMultiModule.configure {
    outputDirectory.set(file("docs"))
    includes.from("README.md")
}
