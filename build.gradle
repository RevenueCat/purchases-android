buildscript {
    ext.kotlinVersion = "1.3.72"
    ext.compileVersion = 28
    ext.minVersion = 14
    repositories {
        jcenter()
        google()
        maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath "com.vanniktech:gradle-maven-publish-plugin:0.9.0"
        classpath "com.android.tools.build:gradle:4.0.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.10.0"
        classpath "org.jacoco:org.jacoco.core:0.8.5"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.7.2"
    id "com.github.kt3k.coveralls" version "2.10.0"
    id "com.savvasdalkitsis.module-dependency-graph" version "0.9"
}

// apply(from = ".buildscripts/jacoco.gradle")

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.8.0"
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task detektAll(type: io.gitlab.arturbosch.detekt.Detekt) {
    description = "Runs over whole code base without the starting overhead for each module."
    buildUponDefaultConfig = true
    autoCorrect = true
    parallel = true
    setSource(files(rootDir))
    include("**/*.kt")
    include("**/*.kts")
    exclude("**/build/**")
    exclude("**/test/**/*.kt")
    baseline.set(file("$rootDir/config/detekt/detekt-baseline.xml"))
    reports {
        xml.enabled = true
        xml.destination = file("build/reports/detekt/detekt.xml")
        html.enabled = false
        txt.enabled = false
    }
}

task detektAllBaseline(type: io.gitlab.arturbosch.detekt.DetektCreateBaselineTask) {
    description = "Overrides current top level baseline with issues found on this run." +
            "Issues found on the baseline will be ignored on detekt runs."
    buildUponDefaultConfig.set(true)
    ignoreFailures.set(true)
    parallel.set(true)
    setSource(files(rootDir))
    baseline.set(file("$rootDir/config/detekt/detekt-baseline.xml"))
    include("**/*.kt")
    include("**/*.kts")
    exclude("**/build/**")
    exclude("**/test/**/*.kt")
}
