buildscript {
    ext.compileVersion = 34
    ext.minVersion = 21
}

plugins {
    alias libs.plugins.mavenPublish apply false
    alias libs.plugins.android.application apply false
    alias libs.plugins.android.library apply false
    alias libs.plugins.kotlin.android apply false
    alias libs.plugins.dokka
    alias libs.plugins.kover apply false
    alias libs.plugins.detekt
    alias libs.plugins.dependencyGraph
    alias libs.plugins.emerge apply false
//    Removing from here gives an error
//    The request for this plugin could not be satisfied because the plugin is already on the classpath with an
//    unknown version, so compatibility cannot be checked.
    alias libs.plugins.kotlin.parcelize apply false
}

dependencies {
    detektPlugins libs.detekt.formatting
    detektPlugins libs.detekt.compose
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task detektAll(type: io.gitlab.arturbosch.detekt.Detekt) {
    description = "Runs over whole code base without the starting overhead for each module."
    buildUponDefaultConfig = true
    autoCorrect = true
    parallel = true
    setSource(files(rootDir))
    include("**/*.kt")
    include("**/*.kts")
    exclude("**/build/**")
    exclude("**/test/**/*.kt")
    exclude("**/testDefaults/**/*.kt")
    exclude("**/testCustomEntitlementComputation/**/*.kt")
    config.setFrom(files("$rootDir/config/detekt/detekt.yml"))
    baseline.set(file("$rootDir/config/detekt/detekt-baseline.xml"))
    reports {
        xml.enabled = true
        xml.destination = file("build/reports/detekt/detekt.xml")
        html.enabled = false
        txt.enabled = false
    }
}

task detektAllBaseline(type: io.gitlab.arturbosch.detekt.DetektCreateBaselineTask) {
    description = "Overrides current top level baseline with issues found on this run." +
            "Issues found on the baseline will be ignored on detekt runs."
    buildUponDefaultConfig.set(true)
    ignoreFailures.set(true)
    parallel.set(true)
    setSource(files(rootDir))
    config.setFrom(files("$rootDir/config/detekt/detekt.yml"))
    baseline.set(file("$rootDir/config/detekt/detekt-baseline.xml"))
    include("**/*.kt")
    include("**/*.kts")
    exclude("**/build/**")
    exclude("**/test/**/*.kt")
    exclude("**/testDefaults/**/*.kt")
    exclude("**/testCustomEntitlementComputation/**/*.kt")
}

tasks.dokkaHtmlMultiModule.configure {
    outputDirectory.set(file("docs/" + project.property("VERSION_NAME")))
    includes.from("README.md")
}
