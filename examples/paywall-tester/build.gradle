plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.ksp)
    alias(libs.plugins.emergetools)
}

emerge {
    // Emerge uses the EMERGE_API_TOKEN env variable by default, so no need to set this explicitly, this is just for example purposes
    apiToken.set(System.getenv("EMERGE_API_TOKEN"))
}

apply from: "$rootProject.projectDir/base-application.gradle"

android {
    namespace 'com.revenuecat.paywallstester'

    defaultConfig {
        applicationId "com.revenuecat.paywall_tester"
        minSdk 24
        versionCode paywallTesterVersionCode as Integer
        versionName paywallTesterVersionName

        // Library modules have a dimension used to separate different apis.
        // Our applications however don't need the extra flavor. This makes sure that we use the
        // default flavor.
        missingDimensionStrategy 'apis', 'defaults'

        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            storeFile file('keystore')
            storePassword project.properties['releaseKeystorePassword']
            keyAlias project.properties['releaseKeyAlias']
            keyPassword project.properties['releaseKeyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.0'
    }
    packaging {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation project(path: ':purchases')
    implementation project(path: ':feature:amazon')
    implementation project(path: ':ui:debugview')
    implementation project(path: ':ui:revenuecatui')

    implementation libs.androidx.core
    implementation platform(libs.kotlin.bom)
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation platform(libs.compose.bom)
    implementation libs.material
    implementation libs.compose.ui
    implementation libs.compose.ui.graphics
    implementation libs.compose.ui.tooling.preview
    implementation libs.compose.material3
    implementation libs.compose.material
    implementation libs.compose.window.size
    implementation libs.navigation.compose
    implementation libs.compose.ui.google.fonts
    debugImplementation libs.compose.ui.tooling
    debugImplementation libs.androidx.test.compose.manifest

    implementation "com.emergetools.snapshots:snapshots-annotations:0.8.3"

    androidTestImplementation "com.emergetools.snapshots:snapshots:0.8.3"
    ksp "com.emergetools.snapshots:snapshots-processor:0.8.3"
}
