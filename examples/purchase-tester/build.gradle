apply from: "$rootProject.projectDir/base-application.gradle"

apply plugin: 'androidx.navigation.safeargs.kotlin'

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'androidx.navigation:navigation-safe-args-gradle-plugin:2.5.3'
    }
}


android {
    buildFeatures {
        dataBinding true
    }
    defaultConfig {
        applicationId "com.revenuecat.purchases_sample"
        minSdkVersion purchaseTesterMinSdkVersion as Integer
        versionCode purchaseTesterVersionCode as Integer
        versionName purchaseTesterVersionName
        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "SUPPORTED_STORES", "\"${purchaseTesterSupportedStores}\""
    }
    signingConfigs {
        release {
            storeFile file('keystore')
            storePassword project.properties['releaseKeystorePassword']
            keyAlias project.properties['releaseKeyAlias']
            keyPassword project.properties['releaseKeyPassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-test-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    testBuildType obtainTestBuildType()
    namespace 'com.revenuecat.purchases_sample'
}

def obtainTestBuildType() {
    def result = "debug"

    if (project.hasProperty("testBuildType")) {
        result = project.getProperties().get("testBuildType")
    }

    result
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(path: ':purchases')
    implementation project(path: ':feature:amazon')
    implementation project(path: ':utils')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.google.android.material:material:1.6.0'
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    implementation "androidx.navigation:navigation-fragment-ktx:2.5.3"
    implementation "androidx.navigation:navigation-ui-ktx:2.5.3"
}
