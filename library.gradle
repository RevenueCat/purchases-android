apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion compileVersion
    buildToolsVersion buildToolsVersion
    flavorDimensions "dependencyVersions"
    productFlavors {
        unityIAP {
            dimension "dependencyVersions"
        }
        latestDependencies {
            dimension "dependencyVersions"
        }
    }
    defaultConfig {
        minSdkVersion minVersion
        targetSdkVersion compileVersion
        versionCode 1
        versionName "5.1.0-SNAPSHOT"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main.java.srcDirs += 'src/main/java'
        test.java.srcDirs += 'src/test/java'
    }
    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.all {
            maxHeapSize = "1024m"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'androidx.test:runner:1.4.0'
    testImplementation 'androidx.test:rules:1.4.0'
    testImplementation 'androidx.test.ext:junit:1.1.3'
    testImplementation 'org.robolectric:robolectric:4.6.1'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.2.0'
    testImplementation 'org.mockito:mockito-core:3.0.0'
    testLatestDependenciesImplementation "com.android.billingclient:billing:$billingVersion"
    testUnityIAPCompileOnly "com.android.billingclient:billing:$billingClient3Version"
    testImplementation 'io.mockk:mockk:1.10.0'
    testImplementation 'org.assertj:assertj-core:3.13.2'
}

def artifactId = project.property("POM_ARTIFACT_ID")
def publishVariant = project.property("PUBLISH_VARIANT")
if (publishVariant == "unityIAPRelease") {
    project.ext.POM_ARTIFACT_ID = artifactId + "-unityiap"
}
println "$project.ext.POM_ARTIFACT_ID"

apply plugin: 'com.vanniktech.maven.publish'

mavenPublish {
    androidVariantToPublish = project.property("PUBLISH_VARIANT")
}
