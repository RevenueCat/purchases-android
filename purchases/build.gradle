plugins {
    alias libs.plugins.android.library
    alias libs.plugins.kotlin.android
    alias libs.plugins.mavenPublish
    alias libs.plugins.dokka
}

apply from: "$rootProject.projectDir/library.gradle"

android {
    namespace 'com.revenuecat.purchases.api'

    productFlavors {
        integrationTest {
            testApplicationId obtainTestApplicationId()
            testBuildType obtainTestBuildType()
            packagingOptions {
                resources.excludes.add("META-INF/LICENSE.md")
                resources.excludes.add("META-INF/LICENSE-notice.md")
            }
        }
    }
}

def obtainTestApplicationId() {
    def result = "com.revenuecat.purchases.integrationTest";

    if (project.hasProperty("testApplicationId")) {
        result = project.getProperties().get("testApplicationId")
    }

    result
}

def obtainTestBuildType() {
    def result = "debug";

    if (project.hasProperty("testBuildType")) {
        result = project.getProperties().get("testBuildType")
    }

    result
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(":common")
    implementation project(":feature:google")
    api project(":public")

    implementation libs.kotlin.stdlib
    implementation libs.androidx.annotation
    api libs.billing
    implementation libs.androidx.lifecycle.runtime
    implementation libs.androidx.lifecycle.common
    implementation libs.androidx.lifecycle.process

    testImplementation project(":test-utils")
    testImplementation libs.bundles.test
    testImplementation libs.billing
    testImplementation libs.coroutines.test

    integrationTestImplementation libs.androidx.appcompat
    integrationTestImplementation libs.material
    integrationTestImplementation libs.androidx.constraintlayout

    androidTestIntegrationTestImplementation libs.androidx.test.espresso.core
    androidTestIntegrationTestImplementation libs.androidx.test.runner
    androidTestIntegrationTestImplementation libs.androidx.test.rules
    androidTestIntegrationTestImplementation libs.androidx.test.junit
    androidTestIntegrationTestImplementation libs.assertJ
    androidTestIntegrationTestImplementation libs.mockk.android
    androidTestIntegrationTestImplementation libs.mockk.agent
}

tasks.dokkaHtmlPartial.configure {
    dokkaSourceSets {
        configureEach {
            reportUndocumented.set(true)
            includeNonPublic.set(false)
            skipDeprecated.set(true)

            externalDocumentationLink {
                url.set(uri("https://developer.android.com/reference/package-list").toURL())
            }
            sourceLink {
                localDirectory.set(file("src/main/kotlin"))
                remoteUrl.set(uri("https://github.com/revenuecat/purchases-android/blob/main/purchases/src/main/kotlin").toURL())
                remoteLineSuffix.set("#L")
            }
        }
    }
}

// Remove afterEvaluate
// after https://github.com/Kotlin/kotlinx-kover/issues/362 is fixed
project.afterEvaluate {
    dependencies {
        kover(project(":common"))
        kover(project(":feature:amazon"))
        kover(project(":feature:google"))
        kover(project(":public"))
        kover(project(":test-utils"))
    }
}
