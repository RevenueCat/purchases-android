apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: "kotlin-android-extensions"
apply plugin: 'kotlin-kapt'
apply from: '../.buildscripts/jacoco.gradle'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName "3.4.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-proguard-rules.pro'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main.java.srcDirs += 'src/main/java'
        test.java.srcDirs += 'src/test/java'
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }
    
    testOptions {
        unitTests.includeAndroidResources = true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation 'androidx.annotation:annotation:1.1.0'
    api 'com.android.billingclient:billing:3.0.0'
    implementation "androidx.lifecycle:lifecycle-runtime:2.1.0"
    implementation "androidx.lifecycle:lifecycle-extensions:2.1.0"
    kapt "androidx.lifecycle:lifecycle-compiler:2.1.0"
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'androidx.test:runner:1.2.0'
    testImplementation 'androidx.test:rules:1.2.0'
    testImplementation 'androidx.test.ext:junit:1.1.1'
    testImplementation 'org.robolectric:robolectric:4.3'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.2.0'
    testImplementation 'org.mockito:mockito-core:3.0.0'
    testImplementation 'com.android.billingclient:billing:3.0.0'
    testImplementation 'io.mockk:mockk:1.10.0'
    testImplementation 'org.assertj:assertj-core:3.13.2'
}

apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'org.jetbrains.dokka'

dokka {
    outputFormat = 'html'
    outputDirectory = "$rootDir/docs"

    configuration {
        moduleName = 'purchases'
        includes = ['README.md']
        reportUndocumented = true
        includeNonPublic = false
        skipDeprecated = true
        // Allows linking to documentation of the project's dependencies (generated with Javadoc or Dokka)
        // Repeat for multiple links
        externalDocumentationLink {
            // Root URL of the generated documentation to link with. The trailing slash is required!
            packageListUrl = new URL("https://developer.android.com/reference/package-list")

            // If package-list file is located in non-standard location
            // packageListUrl = new URL("file:///home/user/localdocs/package-list")
        }

        // Specifies the location of the project source code on the Web.
        // If provided, Dokka generates "source" links for each declaration.
        // Repeat for multiple mappings
        sourceLink {
            // Source directory
            path = "src/main/kotlin"

            // URL showing where the source code can be accessed through the web browser
            url = "https://github.com/revenuecat/purchases-android/blob/master/src/main/kotlin"

            // Suffix which is used to append the line number to the URL. Use #L for GitHub
            lineSuffix = "#L"
        }
        // Suppress a package
        perPackageOption {
            prefix = "com.revenuecat.purchases.util"
            // will match kotlin.internal and all sub-packages of it
            suppress = true
        }
        perPackageOption {
            prefix = "com.revenuecat.purchases"
            // will match kotlin.internal and all sub-packages of it
            suppress = false
        }
    }

}

androidExtensions {
    features = ["parcelize"]
}
